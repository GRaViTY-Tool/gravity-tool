#using org.gravity.tgg.modisco.uml.*
#using AttrCondDefLibrary.*

#abstract #rule AnonymousClass4EnumConst_Abstract #with org.gravity.tgg.modisco.uml

#source { 
	jContainer:EnumDeclaration {
		-enumConstants->mMember
	}
	
	mMember : EnumConstantDeclaration {
		++-anonymousClassDeclaration->mAnon
	}
	
	++mAnon:AnonymousClassDeclaration
}

#target {
	uContainer:Enumeration
	
	++feature:Class
}

#correspondence {
	ne2ne:ASTNode2Element {
		#src->jContainer
		#trg->uContainer
	}

	++a2a:ASTNode2Element{
		#src->mAnon
		#trg->feature
	}
}

#attributeConditions {
	addPrefix(uContainer.name, "Anonymous", feature.name)
}

#using org.gravity.tgg.modisco.uml.*

#rule AnonymousClass4EnumConstInPackage #extends AnonymousClass4EnumConst_Abstract #with org.gravity.tgg.modisco.uml

#source { 
	jPackage: Package
	
	jContainer:EnumDeclaration {
		-package->jPackage
	}
}

#target {
	uPackage:Package
	
	uContainer:Enumeration {
		-package->uPackage
	}
	
	++feature:Class {
	 ++-package->uPackage	
	}
}

#correspondence {
	p2p:JavaPackage2UmlPackage {
		#src->jPackage
		#trg->uPackage
	}
}


#using org.gravity.tgg.modisco.uml.*

#rule AnonymousClass4EnumConstInType  #extends AnonymousClass4EnumConst_Abstract #with org.gravity.tgg.modisco.uml

#source { 
	jType:AbstractTypeDeclaration {
		-bodyDeclarations->jContainer
	}
	
	jContainer:EnumDeclaration
}

#target {
	uContainer:Enumeration {
		-owner->uType
	}
	
	uType:Class {
		++-nestedClassifier->feature
	}
	
	++feature:Class
}

#correspondence {
	p2p:ASTNode2Element {
		#src->jType
		#trg->uType
	}
}
