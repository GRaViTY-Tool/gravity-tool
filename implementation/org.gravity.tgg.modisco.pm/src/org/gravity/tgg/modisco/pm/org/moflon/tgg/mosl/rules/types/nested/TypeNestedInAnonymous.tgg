#using org.gravity.tgg.modisco.pm.*
#using AttrCondDefLibrary.*

#abstract #rule TypeNestedInAnonymous_Abstract
#with org.gravity.tgg.modisco.pm

#source {
	eAnonymousClassDeclaration:AnonymousClassDeclaration

	++mNestedType:AbstractTypeDeclaration {
		++  -anonymousClassDeclarationOwner -> eAnonymousClassDeclaration
	}
}

#target {
	tPackage:TPackage

	tClass:TClass {
		-package -> tPackage
		++-innerTypes -> tNestedType
	}

	pg:TypeGraph {
		-classes -> tClass
		-ownedTypes -> tClass
		++  -ownedTypes -> tNestedType
	}

	++tNestedType:TAbstractType
}

#correspondence {
	++mNestedTypeToTNestedType :TypeToTAbstractType {
		#src->mNestedType
		#trg->tNestedType
	}

	++mASTNodeToAnnotatable :ASTNodeToTAnnotatable {
		#src->mNestedType
		#trg->tNestedType
	}

	eAnonymousClassDeclarationToTClass :AnonymousClassDeclarationToTClass {
		#src->eAnonymousClassDeclaration
		#trg->tClass
	}
}

#attributeConditions {
	concat("$", tClass.tName, mNestedType.name, tNestedType.tName)
}


#using org.gravity.tgg.modisco.pm.*
#using AttrCondDefLibrary.*

#rule InterfaceInAnonymous
#extends TypeNestedInAnonymous_Abstract
#with org.gravity.tgg.modisco.pm

#source {
	++mNestedType:InterfaceDeclaration
}

#target {
	pg:TypeGraph {
		++  -interfaces -> tNestedType
	}

	++tNestedType:TInterface
}


#using org.gravity.tgg.modisco.pm.*
#using AttrCondDefLibrary.*

#rule ClassInAnonymous
#extends TypeNestedInAnonymous_Abstract
#with org.gravity.tgg.modisco.pm

#source {
	++mNestedType:MClass
}

#target {
	pg:TypeGraph {
		++  -classes -> tNestedType
	}

	++tNestedType:TClass
}


#using org.gravity.tgg.modisco.pm.*
#using AttrCondDefLibrary.*

#rule EnumInAnonymous
#extends TypeNestedInAnonymous_Abstract
#with org.gravity.tgg.modisco.pm

#source {
	++mNestedType:EnumDeclaration
}

#target {
	pg:TypeGraph {
		++  -classes -> tNestedType
	}

	++tNestedType:TClass {
		++-tAnnotation -> tEnum
	}

	++tEnum:TEnum
}


#using org.gravity.tgg.modisco.pm.*
#using AttrCondDefLibrary.*

#rule AnnotationTypeInAnonymous
#extends TypeNestedInAnonymous_Abstract
#with org.gravity.tgg.modisco.pm

#source {
	++mNestedType:AnnotationTypeDeclaration
}

#target {
	++tNestedType:TAnnotationType

	pg:TypeGraph {
		++  -tAnnotationTypes -> tNestedType
	}
}


